bundle agent ocemr
{

methods:
                "any" usebundle => ocemr_ocemr();
}

bundle agent ocemr_ocemr
{

classes:
		"apache2_installed"
			expression => fileexists(
				"/usr/sbin/apache2");
		"ocemr_installed"
			expression => fileexists(
				"/usr/share/ocemr/apps/ocemr/__init__.py");
		"mysql_server_installed"
			expression => fileexists(
				"/usr/sbin/mysqld");
	mysql_server_installed::
		"ocemr_database_checked" expression => "any";
		"ocemr_database_exists" expression =>
			fileexists("/var/lib/mysql/$(config.ocemr_db_name)");

methods:

		"any" usebundle => update_package("mysql-server");

		"any" usebundle => update_package("ocemr");

	ocemr_installed::

		"any" usebundle => insert_template(
			"/etc/ocemr/settings.py", "ocemr_settings_py" );

		"any" usebundle => insert_template(
			"/etc/ocemr/util_conf.py", "ocemr_util_conf_py" );

		"any" usebundle => insert_template(
			"/etc/apache2/sites-available/default", "apache2_default" );

	insert_template__etc_ocemr_settings_py_repaired|insert_template__etc_apache2_sites_available_default_repaired::

		"any" usebundle => service("apache2", "reload");

	ocemr_apache_not_running::

		"any" usebundle => service("apache2", "start");

	ocemr_mysqld_not_running::

		"any" usebundle => service("mysql", "start");

commands:

	mysql_server_installed.ocemr_installed.ocemr_database_checked.!ocemr_database_exists::

		"/usr/bin/mysqladmin -uroot create $(config.ocemr_db_name)"
			contain => in_shell;

		"/bin/echo \"GRANT ALL ON $(config.ocemr_db_name).* TO $(config.ocemr_db_user)@$(config.ocemr_db_host) IDENTIFIED BY \\\"$(config.ocemr_db_pass)\\\";\" | /usr/bin/mysql -uroot"
			contain => in_shell;

		"/usr/bin/mysqladmin -uroot reload"
			contain => in_shell;

		"/usr/bin/python /usr/share/ocemr/apps/ocemr/manage.py syncdb --noinput"
			contain => setuid_sh("www-data");

		"/usr/bin/python /usr/share/ocemr/apps/ocemr/manage.py loaddata $(sys.workdir)/inputs/data/default_user.json"
			contain => setuid_sh("www-data");

processes:
	apache2_installed::
		"/usr/sbin/apache2"
			restart_class => "ocemr_apache_not_running";
	mysql_server_installed::
		"/usr/bin/mysqld_safe"
			restart_class => "ocemr_mysqld_not_running";
}
