bundle agent base
{

methods:
		"any" usebundle => base_cfengine3();
		"any" usebundle => base_networking();
		"any" usebundle => base_apt();
		"any" usebundle => base_cups();

}

bundle agent base_cfengine3
{
packages:

		"cfengine3"
			package_policy => "update",
			package_method => generic;

		"ocemr-appliance"
			package_policy => "update",
			package_method => generic;

methods:

		"any" usebundle => insert_template(
			"/etc/default/cfengine3", "cfengine3_default");

	insert_template__etc_default_cfengine3_repaired::
		"any" usebundle => service("cfengine3", "restart");

}


bundle agent base_networking
{
classes:
		"am_gateway"
			expression => strcmp("$(config.base_net_gateway)","");
		"package_dnsmasq" expression => fileexists(
			"/usr/sbin/dnsmasq");

		"package_watch4inet" expression => fileexists(
			"/usr/sbin/watch4inet");

packages:
	am_gateway::
		"dnsmasq"
			package_policy => "addupdate",
			package_method => generic;

		"watch4inet"
			package_policy => "addupdate",
			package_method => generic;

	package_dnsmasq.!am_gateway::
		"dnsmasq"
			package_policy => "delete",
			package_method => generic;

	package_watch4inet.!am_gateway::
		"watch4inet"
			package_policy => "delete",
			package_method => generic;
			
processes:
	am_gateway.package_dnsmasq::
		"/usr/sbin/dnsmasq"
			restart_class => "dnsmasq_not_running";

methods:
		"any" usebundle => insert_template(
			"/etc/network/interfaces", "net_interfaces" );

		"any" usebundle => insert_template(
			"/etc/hostname", "net_hostname" );

		"any" usebundle => insert_template(
			"/etc/mailname", "net_mailname");

		"any" usebundle => insert_template(
			"/etc/hosts", "net_hosts");

		"any" usebundle => insert_template(
			"/etc/resolv.conf", "net_resolv_conf");
	am_gateway::
		"any" usebundle => insert_template(
			"/etc/network/firewall", "net_firewall" );

		"any" usebundle => insert_template(
			"/etc/sysctl.d/ip_forward", "sysctl_d_ip_forward" );

	am_gateway.package_dnsmasq::

		"any" usebundle => insert_template(
			"/etc/dnsmasq.conf", "net_dnsmasq_conf");

	insert_template__etc_network_interfaces_repaired|insert_template__etc_network_firewall_repaired::

		"any" usebundle => restart_interface("$(config.base_net_dev)");

	am_gateway.insert_template__etc_dnsmasq_conf_repaired::

		"any" usebundle => service("dnsmasq", "reload");

	am_gateway.package_dnsmasq.dnsmasq_not_running::

		"any" usebundle => service("dnsmasq", "start");

commands:

	am_gateway.insert_template__etc_sysctl_d_ip_forward_repaired::

		"/sbin/sysctl -p /etc/sysctl.d/ip_forward";

}

bundle agent base_apt
{
classes:
		"package_kremlor_archive_keyring" expression => fileexists(
			"/usr/share/keyrings/kremlor-archive-keyring.gpg");

packages:
		"kremlor-archive-keyring"
			package_policy => "update",
			package_method => generic;

methods:
		"any" usebundle => insert_template(
			"/etc/apt/sources.list", "apt_sources.list");
commands:
	!package_kremlor_archive_keyring::
		"/usr/bin/dpkg -i $(sys.workdir)/inputs/data/kremlor-archive-keyring_2012.03.19squeeze1_all.deb"
			contain => in_shell;
}
bundle agent base_cups
{
classes:
		"package_cups" expression => fileexists(
			"/usr/sbin/cupsd");

packages:
		"cups"
			package_policy => "addupdate",
			package_method => generic;
methods:
	package_cups::
		"any" usebundle => insert_template(
			"/etc/cups/cupsd.conf", "cups_cupsd_conf");

	insert_template__etc_cups_cupsd_conf_repaired::
		"any" usebundle => service("cups", "restart");

	cupsd_not_running::
		"any" usebundle => service("cups", "restart");

processes:
	package_cups::
		"/usr/sbin/cupsd"
			restart_class => "cupsd_not_running";
}
